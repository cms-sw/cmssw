#!/bin/zsh

# DQM daily maintenance script for offline server.
#
# See the operations documentation on how to use this script:
#   https://twiki.cern.ch/twiki/bin/view/CMS/DQMGuiProduction

######################################################################
# Figure out where the server is based on this script's location.
HOST=$(hostname | tr '[:upper:]' '[:lower:]' | sed 's/\..*//')
FILER=/dqmdata
HERE=$(dirname $(dirname $0))
[ X"$HERE" = X ] && { echo "$0: cannot figure out server location"; exit 1; }
[ -d "$HERE/offline/gui" ] || { echo "$0: server location '$HERE' is wrong"; exit 1; }
[ -d "$FILER" ] || { echo "$0: filer location '$FILER' is wrong"; exit 1; }
mkdir $HERE/.nodaily || { echo "$0: daily cannot run"; exit 1; }
trap "rmdir $HERE/.nodaily" 0 1 2 15

######################################################################
for i in offline caf dev; do
  # Purge old sessions.
  find "$HERE/$i/gui/www/sessions" -type f -mmin +120 | xargs rm -f

  # File away server logs.
  for f in $(find $HERE/$i/gui -name '*log-20*.log' -mtime +0 | sort); do
    set -e
    [ -f "$f" ] || continue
    item=${${f#$HERE/$i/gui/}%/*}
    month=$(echo "$f" | perl -pe 's|.*log-||; s|^([0-9]{6}).*|$1|')
    case $month in 20[0-9][0-9][01][0-9] ) ;; * )
      echo "$f: could not determine month from log file name (got: $month)"
      continue ;;
    esac

    mkdir -p $FILER/old-logs/$HOST/$i/$item
    zip -9Tmojq $FILER/old-logs/$HOST/$i/$item/$month.zip $f
    set +e
  done

  # File away collector logs.
  COLLECTOR=$FILER/old-logs/$HOST/collector
  if [ -d $HERE/$i/collector ]; then
    for f in $(find $HERE/$i/collector -name 'collector.out.*' | sort); do
      set -e
      [ -f "$f" ] || continue
      month=$(echo "$f" | perl -pe 's|.*collector\.out\.||; s|^([0-9]{6}).*|$1|')
      case $month in 20[0-9][0-9][01][0-9] ) ;; * )
        echo "$f: could not determine month from log file name (got: $month)"
        continue ;;
      esac

      mkdir -p $COLLECTOR
      zip -9Trmojq $COLLECTOR/collector-$month.zip $f
      set +e
    done
  fi

  # File away agent logs.
  AGENTSDIR=$FILER/old-logs/$HOST/$i/agents
  agents=($(find $HERE/$i/agents -name \*.txt -exec basename {} \; | awk -F \- '{print $1}' | sort -u) )
  for agent in ${agents[@]}; do
    for f in $(find $HERE/$i/agents -name "${agent}-*.txt" -mtime +0 | sort); do
      set -e
      [ -f "$f" ] || continue
      month=$(echo "$f" | perl -pe 's|.*'$agent'-||; s|^([0-9]{6}).*|$1|')
      case $month in 20[0-9][0-9][01][0-9] ) ;; * )
        echo "$f: could not determine month from log file name (got: $month)"
        continue ;;
      esac

      mkdir -p $AGENTSDIR
      zip -9Trmojq $AGENTSDIR/${agent}-$month.zip $f
      set +e
    done
  done
done

######################################################################
for i in offline caf dev; do
  ZIPREPO=$HERE/$i/repository/zipped
  CLEANDIR=$HERE/$i/agents/clean
  
  # Delete transfered zip files
  for cf in $(find $CLEANDIR -name \*.zinfo); do 
    set -e
    ZIPPATH=$ZIPREPO/$(python -c "a=eval(file('$cf').read());print a['zpath']")
    if [[ -e $ZIPPATH ]]
    then 
      rm $ZIPPATH
      rm $cf
    else
      mv $cf ${cf}.bad
    fi
    set +e
  done
done  
